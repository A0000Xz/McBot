apply plugin: "dev.architectury.loom"
apply plugin: "com.replaymod.preprocess"
apply plugin: 'com.github.johnrengelman.shadow'

int mcVersion = 1

preprocess {
    mcVersion = vars.get()["MC"]
    tabIndentation = true
}

configurations {
    shade
    implementation.extendsFrom(shade)
}

repositories {
    mavenCentral()
    maven {
        url = 'https://maven.nova-committee.cn/releases'
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings loom.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"

    shade ("cn.evole.bot:OneBot-Client:${project.onebot_client_version}")
    shade ("blue.endless:jankson:${project.jankson_version}")
    forgeRuntimeLibrary ("cn.evole.bot:OneBot-Client:${project.onebot_client_version}")
    forgeRuntimeLibrary ("blue.endless:jankson:${project.jankson_version}")

    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor "org.projectlombok:lombok:1.18.24"

}

loom {
    silentMojangMappingsLicense()
}

remapJar {
    remapperIsolation = true
}

shadowJar {
    configurations = [project.configurations.shade]
    archiveClassifier.set('')
    destinationDirectory.set(file("$rootDir/build/libs"))
    relocate 'org.java_websocket','cn.evole.websocket'
    relocate 'blue.endless','cn.evole'
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api:1.7.25'))
    }
    dependsOn('remapJar')
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = project.mod_version
group = project.maven_group
archivesBaseName = project.archives_base_name + "-" + project.minecraft_dependency

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version.toString()
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
}

jar {
    from("../../../LICENSE") {
        rename { "${it}_${project.archives_base_name}" }
    }
}